#@TYPE: Distribution
#@NAME: OpenPLi
#@DESCRIPTION: Embedded Linux Distribution Configuration for STB's

require reporefs.conf

DISTRO_FEATURES_BACKFILL_CONSIDERED += "pulseaudio"

DISTRO_NAME ?= "openpli"
DISTRO_HOST ?= "${@os.uname()[1]}"
DISTRO_VERSION ?= "10.3"
DISTRO_TYPE ?= "release"

IPKG_VARIANT = "opkg"
PACKAGE_DEBUG_SPLIT_STYLE = "debug-without-src"

INHERIT += "extrausers"
EXTRA_USERS_PARAMS = "usermod -P dreambox root;"

FEED_NAME ?= "${DISTRO_NAME}-${DISTRO_VERSION}"
DISTRO_FEED_URI ?= "http://${DISTRO_HOST}/feeds/${FEED_NAME}"
DISTRO_FEED_PREFIX ?= "${DISTRO_NAME}"

IMAGE_NAME = "${IMAGE_BASENAME}-${DISTRO_VERSION}-${MACHINE}"
IMAGE_LINK_NAME = ""
CACHE ?= "${TMPDIR}/cache/${LIBC}/${MACHINE}"
DEPLOY_DIR ?= "${TMPDIR}/deploy/${LIBC}"
DEPLOY_DIR_IMAGE = "${DEPLOY_DIR}/images/${MACHINE}"

VIRTUAL-RUNTIME_dev_manager = "busybox-mdev"
VIRTUAL-RUNTIME_login_manager = "busybox"
VIRTUAL-RUNTIME_init_manager = "sysvinit"
VIRTUAL-RUNTIME_initscripts = "initscripts"

PREFERRED_PROVIDER_virtual/enigma2-mediaservice ?= "enigma2-plugin-systemplugins-servicemp3"

PREFERRED_PROVIDERS += "virtual/${TARGET_PREFIX}gcc-initial:gcc-cross-initial"
PREFERRED_PROVIDERS += "virtual/${TARGET_PREFIX}gcc-intermediate:gcc-cross-intermediate"
PREFERRED_PROVIDERS += "virtual/${TARGET_PREFIX}gcc:gcc-cross"
PREFERRED_PROVIDERS += "virtual/${TARGET_PREFIX}g++:gcc-cross"

# Gstreamer 1.x
GST_VERSION = "1.0"

# python
PYTHON_PN = "python"
PYTHON_BASEVERSION = "2.7"
PREFERRED_VERSION_python = "2.7.18"
PREFERRED_VERSION_python-native = "2.7.18"

# other
PREFERRED_VERSION_tzdata = "2017b"
PREFERRED_VERSION_sdparm = "1.07"
PREFERRED_VERSION_mtd-utils = "2.0.0"
PREFERRED_VERSION_python-wifi = "0.6.1"
PREFERRED_VERSION_transmission = "3.00"
PREFERRED_VERSION_python-websocket-client = "0.59.0"

PREFERRED_PROVIDER_libcrypto ?= "openssl"
PREFERRED_PROVIDER_libssl ?= "openssl"
PREFERRED_RPROVIDER_libltdl ?= "libtool"
PREFERRED_RPROVIDER_ssh ?= "openssh"
PREFERRED_PROVIDER_libav ?= "ffmpeg"
PREFERRED_PROVIDER_python-pycrypto ?= "python-pycryptodome"

PREFERRED_VERSION_wpa-supplicant = "2.9"
PREFERRED_VERSION_directfb_arm = "1.7.7"
PREFERRED_VERSION_directfb-native_arm = "1.7.7"
PREFERRED_VERSION_directfb_aarch64 = "1.7.7"
PREFERRED_VERSION_directfb-native_aarch64 = "1.7.7"
PREFERRED_VERSION_libsoup-2.4 = "2.62.3"
PREFERRED_VERSION_libsdl2 = "2.0.14"
PREFERRED_VERSION_libsdl2_mipsel = "2.0.8"
PREFERRED_VERSION_libsdl2_mips = "2.0.8"

LICENSE_FLAGS_WHITELIST = "commercial"
COMMERCIAL_AUDIO_PLUGINS ?= "gstreamer1.0-plugins-good-audioparsers gstreamer1.0-plugins-good-mpg123"
COMMERCIAL_VIDEO_PLUGINS ?= "gstreamer1.0-plugins-ugly-mpeg2dec gstreamer1.0-plugins-bad-videoparsersbad gstreamer1.0-plugins-bad-mpegtsdemux gstreamer1.0-plugins-bad-mpegpsdemux"

# OE optimization defaults to -O2 which makes for much larger binaries.
# Overridden here to use -Os instead, resulting in smaller images.
# Overridden here to use -O3 instead for better performance.
FULL_OPTIMIZATION = "-Os -pipe ${DEBUG_FLAGS}"
FULL_OPTIMIZATION_dm900 = "-O2 -pipe ${DEBUG_FLAGS}"
FULL_OPTIMIZATION_dm920 = "-O2 -pipe ${DEBUG_FLAGS}"

DISTRO_FEATURES ?= "alsa bluetooth directfb ext2 largefile wifi nfs zeroconf mplt pam ${DISTRO_FEATURES_LIBC} ipv4 ipv6 tuxtxtfhd"

QA_LOG = "1"
QA_LOGFILE = "${TMPDIR}/qa.log"

OEINCLUDELOGS ?= "yes"
KERNEL_CONSOLE ?= "ttyS0,115200n8"

BB_GENERATE_MIRROR_TARBALLS ??= "0"

NO32LIBS ??= "1"

# Missing checksums should raise an error
BB_STRICT_CHECKSUM = "0"

# Place output in "tmp" as before, not "tmp-glibc"
TCLIBCAPPEND = ""

# Don't grab feeds for lower level archs, e.g. mips32el should not
# attempt to fetch mipsel, and armv7 need not fetch armv5 etcetera.
DISTRO_FEED_ARCHS = "all ${TUNE_PKGARCH} ${MACHINE_ARCH}"

# Use OpenSSL only
PACKAGECONFIG_pn-curl = "${@bb.utils.contains("DISTRO_FEATURES", "ipv6", "ipv6", "", d)} ssl proxy zlib libssh2"
PACKAGECONFIG_pn-openldap = "openssl modules mdb ldap meta monitor null passwd shell proxycache dnssrv ${@bb.utils.contains('DISTRO_FEATURES', 'ipv6', 'ipv6', '', d)}"
PACKAGECONFIG_pn-neon = "expat openssl libproxy webdav zlib"
